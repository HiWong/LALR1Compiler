<expression> ::= <assignment_expression> ;
<expression> ::= <expression> "," <assignment_expression> ;
<variable_identifier> ::= identifier ;
<primary_expression> ::= <variable_identifier> ;
<primary_expression> ::= number ;
<primary_expression> ::= number ;
<primary_expression> ::= number ;
<primary_expression> ::= <BOOLCONSTANT> ;
<primary_expression> ::= number ;
<primary_expression> ::= "(" <expression> ")" ;
<BOOLCONSTANT> ::= "true" ;
<BOOLCONSTANT> ::= "false" ;
<postfix_expression> ::= <primary_expression> ;
<postfix_expression> ::= <postfix_expression> "[" <integer_expression> "]" ;
<postfix_expression> ::= "function_call" ;//TODO: 暂时没有function_call
<postfix_expression> ::= <postfix_expression> "." <FIELD_SELECTION> ;
<postfix_expression> ::= <postfix_expression> "++" ;
<postfix_expression> ::= <postfix_expression> "--" ;
<FIELD_SELECTION> ::= identifier ;
<integer_expression> ::= <expression> ;
<unary_expression> ::= <postfix_expression> ;
<unary_expression> ::= "++" <unary_expression> ;
<unary_expression> ::= "--" <unary_expression> ;
<unary_expression> ::= <unary_operator> <unary_expression> ;
<unary_operator> ::= "+" ;
<unary_operator> ::= "-" ;
<unary_operator> ::= "!" ;
<unary_operator> ::= "~" ;
<multiplicative_expression> ::= <unary_expression> ;
<multiplicative_expression> ::= <multiplicative_expression> "*" <unary_expression> ;
<multiplicative_expression> ::= <multiplicative_expression> "/" <unary_expression> ;
<multiplicative_expression> ::= <multiplicative_expression> "%" <unary_expression> ;
<additive_expression> ::= <multiplicative_expression> ;
<additive_expression> ::= <additive_expression> "+" <multiplicative_expression> ;
<additive_expression> ::= <additive_expression> "-" <multiplicative_expression> ;
<shift_expression> ::= <additive_expression> ;
<shift_expression> ::= <shift_expression> "<<" <additive_expression> ;
<shift_expression> ::= <shift_expression> ">>" <additive_expression> ;
<relational_expression> ::= <shift_expression> ;
<relational_expression> ::= <relational_expression> "<" <shift_expression> ;
<relational_expression> ::= <relational_expression> ">" <shift_expression> ;
<relational_expression> ::= <relational_expression> "<=" <shift_expression> ;
<relational_expression> ::= <relational_expression> ">=" <shift_expression> ;
<equality_expression> ::= <relational_expression> ;
<equality_expression> ::= <equality_expression> "==" <relational_expression> ;
<equality_expression> ::= <equality_expression> "!=" <relational_expression> ;
<and_expression> ::= <equality_expression> ;
<and_expression> ::= <and_expression> "&" <equality_expression> ;
<exclusive_or_expression> ::= <and_expression> ;
<exclusive_or_expression> ::= <exclusive_or_expression> "^" <and_expression> ;
<inclusive_or_expression> ::= <exclusive_or_expression> ;
<inclusive_or_expression> ::= <inclusive_or_expression> "|" <exclusive_or_expression> ;
<logical_and_expression> ::= <inclusive_or_expression> ;
<logical_and_expression> ::= <logical_and_expression> "&&" <inclusive_or_expression> ;
<logical_xor_expression> ::= <logical_and_expression> ;
<logical_xor_expression> ::= <logical_xor_expression> "^^" <logical_and_expression> ;
<logical_or_expression> ::= <logical_xor_expression> ;
<logical_or_expression> ::= <logical_or_expression> "||" <logical_xor_expression> ;
<conditional_expression> ::= <logical_or_expression> ;
<conditional_expression> ::= <logical_or_expression> "?" <expression> ":" <assignment_expression> ;
<assignment_expression> ::= <conditional_expression> ;
<assignment_expression> ::= <unary_expression> <assignment_operator> <assignment_expression> ;
<assignment_operator> ::= "=" ;
<assignment_operator> ::= "*=" ;
<assignment_operator> ::= "/=" ;
<assignment_operator> ::= "%=" ;
<assignment_operator> ::= "+=" ;
<assignment_operator> ::= "-=" ;
<assignment_operator> ::= "<<=" ;
<assignment_operator> ::= ">>=" ;
<assignment_operator> ::= "&=" ;
<assignment_operator> ::= "^=" ;
<assignment_operator> ::= "|=" ;


